apply plugin: 'groovy'
apply plugin: 'maven-publish'


group = 'com.meituan.gradle'
version = '0.9.1'
description = 'A Gradle plugin to do AspectJ binary weaving in the way of Android class -> dex transform. It works as an augment for Android plugin'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    jcenter()
    mavenLocal()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'commons-io:commons-io:2.4'
    compile 'com.android.tools.build:gradle:2.1.0'
    compile 'org.aspectj:aspectjtools:1.8.9'
}

// publish
final boolean DEBUG = false;

task docJar(type: Jar, dependsOn: javadoc) {
    from tasks.javadoc.destinationDir
    classifier 'doc'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

def isSnapshot() {
    return version.endsWith("SNAPSHOT")
}

publishing {
    repositories {
        maven {
            if (!DEBUG) {
                if (isSnapshot()) {
                    url mavenSnapshotRepo
                } else {
                    url mavenReleaseRepo
                }
                credentials {
                    username = mavenUsername
                    password = mavenPassword
                }
            } else {
                url "${buildDir}/libs"
            }
        }
    }

    publications {
        RoboAspectJ(MavenPublication) {
            from components.java
            artifactId = 'roboaspectj'
            artifact docJar
            artifact sourceJar
            pom.withXml {
                asNode().appendNode('description', '${description}')
            }
        }
    }
}